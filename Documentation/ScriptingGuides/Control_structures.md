# Управляющие структуры

Управляющие структуры - это инструкции, которые используются для управления потоком выполнения в коде. Они представляют собой последовательности скриптового кода, которые помогают управлять сложными процедурами. Вы можете использовать управляющие структуры для определения кода, который выполняется только при определенных условиях или повторяется несколько раз.

> Примечание для продвинутых читателей: 
*На языке SQF управляющие структуры - это обычные скриптовые команды, не требующие специальной обработки по сравнению с другими командами. Это отличается от большинства императивных языков программирования (таких как C), где управляющие инструкции реализованы в грамматике основного языка. Осуществляемое ими "управление" осуществляется путем принятия кода в качестве аргумента. Сложные структуры управления, такие как "for... "do..." реализуются с использованием вспомогательных типов, например таких как WhileType.*

## Операторы
| Оператор | Описание |
| -------- | -------- |
| **Математические** | |
| + | Сложение |
| - | Вычитание |
| / | Деление |
| * | Умножение |
| ^ | Возведение в степень |
| % | Остаток от деления |
| **Логические** | |
| == | сравнение РАВНО; Левый и правый операнды должны быть одинакового типа: 1 == 1|
| != | сравнение НЕ РАВНО; ; Левый и правый операнды должны быть одинакового типа: 1 != -1|
| > | сравнение БОЛЬШЕ; Левый и правый операнды должны быть одинакового типа: 4.5 > 4|
| < | сравнение МЕШЬНЕ; Левый и правый операнды должны быть одинакового типа: -3 < 3|
| >= | сравнение БОЛЬШЕ ИЛИ РАВНО; Левый и правый операнды должны быть одинакового типа: 5 >= 1|
| <= | сравнение МЕНЬШЕ ИЛИ РАВНО; Левый и правый операнды должны быть одинакового типа: 0 <= 120.3|
| !a | логическое НЕ; !true == false|
| && | логическое И; true && false будет false |
| \|\| | логическое ИЛИ; true \|\| false будет true|




# Условные структуры
Условные структуры помогают определить код, который выполняется только при определенных обстоятельствах. Часто вы будете писать код, который зависит от состояния игры, от другого кода или других условий.

## Оператор if
Оператор `if` определяет код, который выполняется только если выполнено определенное условие. Синтаксис этого выглядит следующим образом:
```sqf
if (CONDITION) then {
	STATEMENT;
	...
};
```
**CONDITION** является логическим заявлением или переменной которая возвращает либо true или false. Код, вложенный в следующее блок выполняется только в том случае, если выполнено условие true, иначе будет проигнорировано.
**STATEMENT** представляет собой пользовательскую последовательность заявлений. Это могут быть команды, назначения, структуры управления и т.д.

```sqf
if (_temperature < 0) then
{
	["Snow!"] call messageBox;
};
```
В этом примере сначала проверяется значение *_temperature*:

Если значение равно 0 или больше 0, вложенный код игнорируется.
Если значение меньше 0, то выведится сообщение "Snow!"

# else
Вы также можете определить альтернативный код, который выполняется, когда условие не выполняется.
```sqf
if (CONDITION) then 
{
	STATEMENT1;
	...
} else {
	STATEMENT2;
	...
};
```
## Условное назначение
if..then структуры также могут использоваться для присвоения условных значений переменным:
```sqf

//... Где-то выше у нас определилась своя переменная _isAlive

_living = if (_isAlive) then { true } else { false };

//Альтернатива с использованием макроса (определён в engine.hpp)
_living = ifcheck(_isAlive,true,false);

```
## Вложенные if

Вы также можете определить один оператор if, вложенный в другой: 
```sqf
if (_isAlive) then {
	if (_hasAmmo) then {
		["player is is alive and has ammo"] call messageBox; 
	} else {
		["player is out of ammo"] call messageBox;
	};
} else {
	["player dead"] call messageBox;
};
```

## Расширенный пример с использованием if и локальных переменных


```sqf
private _a = 2; private _b = 3; 
if (_b > _a) then {
	_a = 10;
};
// _a == 10; _b == 3
if (_b <= _a) then {
	_b = 11;
} else {
	_a = 0;
};
// _a == 10; _b == 11
//------------------
call {
	if (_a == 0) exitWith {
		_b = 1;
	};
	if (_a == 5 && _b == 11) exitWith {
		_b = 10;
	};
	if (_a == 10 || _b == 11) exitWith {
		_b = 5;
	};
};
// _a == 10; _b == 5
//------------------
call {
	if (_a == 1) exitWith {
		_b = 1;
	};
	if (_a == 5) then {
		_b = 2;
	} else {
		_b = 10;
	};
};
// _a == 10; _b == 10;
```

### Оператор exitWith

Выходит из текущей области кода. Часто используется для выхода из do, for, count, forEach или всего скрипта.

```sqf
private _condition = true;

if (_condition) then
{
	if (true) exitWith
	{
		["exiting if _condition scope"] call messageBox;
	};
	["never shown"] call messageBox;
};
```

# Циклы

## Оператор while
**!Внимание!**
> Количество итераций в операторе while ограничено 10000. При достижении лимита будет произведен автоматический выход из цикла.


```sqf
private _counter = 0;
while {_counter < 50} do {
	_counter = _counter + 1;
};
// _counter == 49;
```

## Оператор for

```sqf
private _list = [2,4,3,8];
private _t = 0;
for "_index" from 0 to (count _list) - 1 step 1 do {
	_t = _list select _index;
	// Проверка если число чётное (делится на 2 без остатка)
	if (_t % 2 == 0) then {
		_list set [_index,_t - 2];
	} else {
		_list set [_index, -_t];
	};
};
// _list == [0,2,-3,6]
```

> *count* - унарный оператор для подсчета количества элементов массива или символов в строке: 
>> count [5,7,10] == 3

**Внимание!**
> Размер шага - **step** может быть опущен. Тогда в этом случае указанная переменная оператора for будет увеличиваться на 1 при каждой итерации.

## Оператор foreach
[Подробнее о данном операторе](https://community.bistudio.com/wiki/forEach)

```sqf
private _list = [4,20,5,30,6,40];
private _countAllLessTen = 0;
{
	if (_x <= 10) then {
		_countAllLessTen = _countAllLessTen + _x;
		//кстати вместо _x можно было бы написать (_list select _foreachIndex)
	};
} foreach _list;
// _countAllLessTen == 15
```

### Выход из циклических операторов и пропуск итераций

Для дальнейшей работы с циклическими операторами типа **while**, **for** и **foreach** посетите следующие страницы:
- [break](https://community.bistudio.com/wiki/break)
- [continue](https://community.bistudio.com/wiki/continue)

# [Следующий раздел](Functions.md)